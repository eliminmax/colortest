(Note - this is "minimalist", rather than "idiomatic", Rockstar
By default, running Satrani, the reference implementation of Rockstar
, prints out "program returned no output" at the end of the execution.
I am using Satrani as of Git commit #2f53845 of the repo git@github.com:RockstarLang/rockstar,
with line 17 of satrani/rockstar.js commented out, so that that doesn't mess up the output of this
program.)

Esc is 27
Cast Esc

ClearSequence is ""
Let ClearSequence be with Esc
Let ClearSequence be with "[0m"

CellMaker takes CellNumber
CellString is ""
Let CellString be with Esc
Let CellString be with "[48;5;"
Let CellString be with CellNumber
Let CellString be with "m  "
give back CellString
(blank/comment only line ends the function block)

(Print the first 16 colors - these vary by terminal configuration)
PrintBuffer is ""
i is 0
while i is weaker than 16
put PrintBuffer with CellMaker taking i into PrintBuffer
build i up
(blank/comment only line ends the loop block)
put PrintBuffer with ClearSequence into PrintBuffer
say ""
say PrintBuffer
say ""


( Print the 6 sides of the color cube - these are more standardized,
but the order is a bit odd, thus the need for this trickery)
i is 16
while i is weaker than 52
PrintBuffer is ""
ii is 0
while ii is weaker than 6
put i with ii into iii
put PrintBuffer with CellMaker taking iii into PrintBuffer
build ii up
(blank/comment only line ends the inner loop block)
Let PrintBuffer be with ClearSequence
Let PrintBuffer be with "  "
ii is 36
while ii is weaker than 42
put i with ii into iii
put PrintBuffer with CellMaker taking iii into PrintBuffer
build ii up
(blank/comment only line ends the inner loop block)
Let PrintBuffer be with ClearSequence
Let PrintBuffer be with "  "
ii is 72
while ii is weaker than 78
put i with ii into iii
put PrintBuffer with CellMaker taking iii into PrintBuffer
build ii up
(blank/comment only line ends the inner loop block)
Let PrintBuffer be with ClearSequence
say PrintBuffer
build i up, up, up, up, up, up
(blank/comment only line ends the outer loop block)
say ""

i is 124
while i is weaker than 160
PrintBuffer is ""
ii is 0
while ii is weaker than 6
put i with ii into iii
put PrintBuffer with CellMaker taking iii into PrintBuffer
build ii up
(blank/comment only line ends the inner loop block)
Let PrintBuffer be with ClearSequence
Let PrintBuffer be with "  "
ii is 36
while ii is weaker than 42
put i with ii into iii
put PrintBuffer with CellMaker taking iii into PrintBuffer
build ii up
(blank/comment only line ends the inner loop block)
Let PrintBuffer be with ClearSequence
Let PrintBuffer be with "  "
ii is 72
while ii is weaker than 78
put i with ii into iii
put PrintBuffer with CellMaker taking iii into PrintBuffer
build ii up
(blank/comment only line ends the inner loop block)
Let PrintBuffer be with ClearSequence
say PrintBuffer
build i up, up, up, up, up, up
(blank/comment only line ends the outer loop block)

( Finally, the 24 grays )
PrintBuffer is ""
i is 232
while i is weaker than 256
put PrintBuffer with CellMaker taking i into PrintBuffer
build i up

put PrintBuffer with ClearSequence into PrintBuffer
say ""
say PrintBuffer
say ""
