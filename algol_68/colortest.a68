# SPDX-FileCopyrightText: 2023-2024 Eli Array Minkoff #
# SPDX-License-Identifier: GPL-3.0-only #

CHAR esc;
esc := REPR 27;

PROC color_cell = (INT n) VOID:
    print((esc, "[48;5;", whole(n, 0), "m  "));

PROC cube_row_part = (INT n) VOID:
    FOR i FROM n TO (n + 5) DO color_cell(i) OD;

PROC cube_row = (INT n) VOID:
    BEGIN
        cube_row_part(n);
        print((esc, "[0m  "));
        cube_row_part(n + 36);
        print((esc, "[0m  "));
        cube_row_part(n + 72);
        print((esc, "[0m", newline))
    END;
    

# Print the first 16 colors - these vary by terminal configuration #
print(newline);
FOR i FROM 0 TO 15 DO color_cell(i) OD;

print((esc, "[0m", newline, newline));

# Print the 6 sides of the color cube - these are more standardized,
but the order is a bit odd, thus the need for this trickery #
FOR i FROM 16 BY 6 TO 46 DO cube_row(i) OD;
print(newline);
FOR i FROM 124 BY 6 TO 154 DO cube_row(i) OD;
print(newline);

# Finally, the 24 grays #
FOR i FROM 232 TO 255 DO color_cell(i) OD;
print((esc, "[0m", newline, newline))
