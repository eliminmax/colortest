v             >">"67*5p                                   88+10p v  print the color cube   
              $  reroute colorcell exit                 >                                 v
>55+, v>52*:,,^   v                       <              v*7+5601<      < <                
 v:+88<^,,,<          >"0"+,v >v>:52*/"0"v               >    :v                           
v_39*,"m0["^        >v/     # \`^<v%*52,+<               v*89  <                           
>:88+\-  v >       v  ^*:*52:<*!                        ^> `! v^ :-1\<                     
 # v     < ^   <    :5      # 5>| >"0"+,v ^              vg01 _ :10g+^v<                   
   >           ^    ^>2*:*\`!|2 > ^ v" "<                +                ^,+55p+77+<      
  v       <          v%*:*25<:      >" m"v               >0v              >">"3374**^      
  >   v              >           ^   v,,,<               v0<              ^p+76*8+55"v"<   
 ^ :-1<                      >^      >    ^              >85*1+:v         >">"374**76+p^   
          ^       < ^,,,,,,,*39"[48;5;"<                 v!`*94 <:<       ^  p01*4-1*48 <  
  print the first  >                   ^                 >v       -     ^           p01<   
  16 colors          colorcell                           v_:10g+\1^           >:85+4*\`|   
                                                         >10g+005v   v<       + 8>v    $ v 
                                                   v   <    v`\0:<   >                v>^  
                                                          v$_:10v             6 5*4        
                                                             v+g<             g >^>*^      
                                                             >\1-^   v  #    ,,,,,   <     
                                                          >          >:|      #            
                                                                       >$v    0            
                                                                       v <    1            
           ^ +*<                                                 v ,*39_39*," "" m0["^     
  >:46*\-856*1-^   ^       <                              ^                   #           <
  ^                    <    v-1                    <             >"m0[",,,55+,^            
                       |<  ^                                                          <    
    @,,:+55,,,"[0m",*93<^:  <                                *64p01*8-1*53p5*5+56"v",+55$< 
   finally, the 24 grays    after the first color cube                  replace the cell   
                            loop, set the following 4  124,y=0,x=1      west of the one    
                            cell values to change the  ">",y=14,x=87    changed after the  
                            starting and ending values "v",y=13,x=80    1st 16 colors after
                            of the loop                ">",y=13,x=84    the cube is done   
###########################################################################################
vi: ft= sw=1 ts=1 sts=1 et cc=5,10,15,20,25,30,35,40,45,50,55,60,65,70,75,80,85,90 nowrap  
